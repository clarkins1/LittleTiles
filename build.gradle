plugins {
    id 'eclipse'
    id 'idea'
    id 'com.modrinth.minotaur' version '2.+'
    id 'net.neoforged.gradle.userdev' version '7.0.+'
}

repositories {
    maven {
        url "https://maven.su5ed.dev/releases"
    }
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        url = "https://api.modrinth.com/maven"
    }
    maven { url = 'https://maven.fabricmc.net' }
}

group= "team.creative" + project.mod_id

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

java.sourceSets.create("api") {
    
    java {
        srcDir("src/api/java")
    }
    compileClasspath += sourceSets.main.compileClasspath
    runtimeClasspath += sourceSets.main.runtimeClasspath
}

sourceSets.main.compileClasspath += sourceSets.api.output
sourceSets.main.runtimeClasspath += sourceSets.api.output

project.evaluationDependsOn(":CreativeCore")

runs {  
    configureEach {
        workingDirectory project.file('run')
        systemProperty 'forge.logging.console.level', 'debug'
    }
    
    client {
    }
    
    server {
    }
}

dependencies {
    implementation "net.neoforged:neoforge:${forge_version}"
    
    compileOnly "maven.modrinth:sodium:r0Cvb6jf"
    compileOnly "curse.maven:theoneprobe-245211:5502323"
    
    implementation project(':CreativeCore')
    
    compileOnly "net.fabricmc:fabric-loader:${fabric_version}"
    compileOnly "org.sinytra.forgified-fabric-api:fabric-api-base:0.4.42+d1308ded19"
    compileOnly "org.sinytra.forgified-fabric-api:fabric-renderer-api-v1:3.4.0+acb05a3919"
    compileOnly "org.sinytra.forgified-fabric-api:fabric-rendering-data-attachment-v1:0.3.48+73761d2e19"
    compileOnly "org.sinytra.forgified-fabric-api:fabric-block-view-api-v2:1.0.10+9afaaf8c19"
}

modDependencies = [
    [ modId: "creativecore", type: "required", ordering: "NONE", side: "BOTH", versionRange: "[" + creativecore_version + ",)" ]
]
modMixins = [ "littletiles.mixins.json" ]
